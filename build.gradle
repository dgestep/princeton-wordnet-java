plugins {
    id 'org.springframework.boot' version '2.6.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.estepsoftwareforensics'
version = '1.0.0'
targetCompatibility = 1.11
sourceCompatibility = 1.11
ext.moduleName = 'estep.princeton.wordnet'

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter:2.6.3'
    implementation 'org.apache.opennlp:opennlp-tools:1.9.3'

    testImplementation 'commons-io:commons-io:2.11.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.6.3'
}

test {
    useJUnitPlatform()
}
// disabling this allows the app to build without the need of a Spring Boot Main class.
bootJar {
    enabled = false
}
// enabling this tells Gradle just to build a JAR and not a Spring Boot enabled JAR.
jar {
    enabled = true

    inputs.property("moduleName", moduleName)

    manifest {
        attributes 'Automatic-Module-Name': moduleName
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'com.estepsoftwareforensics:princeton-wordnet-java'
                description = 'Princeton WordNet for Java is a simple API which provides access to an english ' +
                        'dictionary that is backed by the Princeton University WordNet datasets. This is intended to ' +
                        'be a third-party supporting library to any application that requires an english word ' +
                        'dictionary -- providing the ability to lookup a word, get its many definitions, and get ' +
                        'the list of synonyms and any related words.'
                url = 'https://estepsoftwareforensics.com'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'douglas.estep'
                        name = 'Doug Estep'
                        email = 'douglas.estep@gmail.com'
                        organization = 'Estep Software Forensics'
                        organizationUrl = 'https://estepsoftwareforensics.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/dgestep/princeton-wordnet-java.git'
                    developerConnection = 'scm:git:https://github.com/dgestep/princeton-wordnet-java.git'
                    url = 'https://estepsoftwareforensics.com'
                }
            }
        }
    }
    repositories {
        maven {
            // uncomment out if publishing to local maven repository
//            url = layout.buildDirectory.dir('libs')

            // publishing to maven centry
            url = project.property('artificatoryContextUrl')
            credentials {
                username = project.property('artifactoryUserName')
                password = project.property('artifactoryPassword')
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}